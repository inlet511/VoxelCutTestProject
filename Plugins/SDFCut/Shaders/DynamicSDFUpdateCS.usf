
#include "/Engine/Public/Platform.ush"
#include "/Engine/Private/Common.ush"

Texture3D<float> InputSDF;
SamplerState InputSDFSampler;
Texture3D<float> ToolSDF;
SamplerState ToolSDFSampler;
RWTexture3D<float> OutputSDF;

// Uniform Buffer
cbuffer CutUB
{
    float3 TargetLocalBoundsMin;
    float3 TargetLocalBoundsMax;
    float3 ToolLocalBoundsMin;
    float3 ToolLocalBoundsMax;
    float4x4 TargetToToolTransform;
    int3 SDFDimensions;
    int3 UpdateRegionMin;
    int3 UpdateRegionMax;
};

// 采样SDF纹理（将世界坐标转换为纹理UV）
float SampleSDF(Texture3D<float> SDFTex, SamplerState Sampler, float3 Pos, float3 SDFBoundsMin, float3 SDFBoundsMax)
{
    float3 UV = saturate((Pos - SDFBoundsMin) / (SDFBoundsMax - SDFBoundsMin));
    return SDFTex.SampleLevel(Sampler, UV, 0).r;
}

// 通过DispatchedThreadID获取它在SDF局部坐标系的坐标
float3 VoxelToObjectPos(int3 GlobalVoxelID, int3 SDFDimensions, float3 TargetLocalBoundsMin, float3 TargetLocalBoundsMax)
{
    float3 Step = (TargetLocalBoundsMax - TargetLocalBoundsMin) / (float3) SDFDimensions;
    return TargetLocalBoundsMin + (GlobalVoxelID + 0.5f) * Step;
}

// 检查一个点是否在一个范围内
bool IsInToolLocalBounds(float3 Pos, float3 BoundsMin, float3 BoundsMax)
{
    return
        Pos.x >= BoundsMin.x
        && Pos.y >= BoundsMin.y
        && Pos.z >= BoundsMin.z
        && Pos.x <= BoundsMax.x
        && Pos.y <= BoundsMax.y
        && Pos.z <= BoundsMax.z;
}

float3 GetToolUV(float3 PosInToolSpace, float3 ToolLocalBoundsMin, float3 ToolLocalBoundsMax)
{
    return (PosInToolSpace - ToolLocalBoundsMin)/(ToolLocalBoundsMax - ToolLocalBoundsMin);
}

[numthreads(4, 4, 4)]
void LocalSDFUpdateKernel(uint3 DispatchThreadID : SV_DispatchThreadID)
{
    // 转换为全局体素坐标
    int3 GlobalVoxelID = UpdateRegionMin + DispatchThreadID;
    
    // 边界检查
    if (any(GlobalVoxelID > UpdateRegionMax) || any(GlobalVoxelID < int3(0,0,0)))
        return;
    
    // 1. 计算物体局部坐标
    float3 TargetPosInLocalSpace = VoxelToObjectPos(GlobalVoxelID, SDFDimensions, TargetLocalBoundsMin, TargetLocalBoundsMax);
    
    // 2. 采样原始物体SDF（直接在物体局部坐标系）
    float OriginalSDFValue = SampleSDF(InputSDF, InputSDFSampler,TargetPosInLocalSpace, TargetLocalBoundsMin, TargetLocalBoundsMax);
    
    // 3. 将物体局部坐标转换到工具局部坐标系
    float4 ToolSpacePos4 = mul(TargetToToolTransform, float4(TargetPosInLocalSpace, 1.0));
    float3 ToolSpacePos = ToolSpacePos4.xyz / ToolSpacePos4.w;

    float ResultValue = OriginalSDFValue;
    
    // 4. 检查是否在工具边界内（快速剔除）
    if (IsInToolLocalBounds(ToolSpacePos, ToolLocalBoundsMin, ToolLocalBoundsMax))
    {
        // 5. 采样工具SDF（工具局部坐标系）
         float3 ToolUV = GetToolUV(ToolSpacePos, ToolLocalBoundsMin, ToolLocalBoundsMax);
        float ToolSDFValue = ToolSDF.SampleLevel(ToolSDFSampler, ToolUV, 0);    
        
        ResultValue = (ToolSDFValue<0) ? -ToolSDFValue: OriginalSDFValue;
        
        // float Distance = distance(ToolUV, float3(0.5,0.5,0.5));
        //  if (Distance < 0.005)
        //  {
        //      // 挖掉
        //      ResultValue = 0.1;
        //  }
    }

    // 7. 写入结果
    OutputSDF[GlobalVoxelID] = ResultValue;
}